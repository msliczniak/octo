# A B T
# ---+-
# 0 0 E
# 0 1 V
# 1 0 R
# 1 1 L

# A B C
# ---+-
# 0 0 E
# 0 1 B
# 1 0 Y
# 1 1 R

:org 0xdb
: bottle-16
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

: bottle
0 0 0 0  0 0 0 0

: hand
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

: zeros
0 0 0 0  0 0

: bottles-4
0 0  0 8

: bottles
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

: bottles_40
0x00 0x00 0xff

0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0

: main-8
0 0 0 0  0 0 0 0

:org 0x200
: main
jump _zero
:byte 0xff

: str

:org 0x210
: _zero
v9 := 8
i := zeros
save v9

v8 := 0x12
v9 := 0x1e
i := main-8
save v9

:org 0x21e
hires

: _main
clear

:const SEEDM 0
:const SEEDL 2
:const LVLA 20
:const LVLB 0

:alias SPARE v0
:alias FOURTEEN v1
:alias LFSRM v2
:alias LFSRL v3
:alias FOUR  v4
:alias LVL   v5
:alias POS   v6
:alias TYPE  v7
:alias MSK   v8
:alias ENDL   v9

:alias YPOS  va
:alias VIRI  vb

:alias LFSR0 vd
:alias LFSR1 ve
:alias FLAG  vf

LFSR0 := SEEDM
LFSR1 := SEEDL

vf := 255
delay := vf

: repeat
vf := delay
if vf != 253
then jump repeat

YPOS := 2
VIRI := LVLA
:call gen_bottle

YPOS := 61
VIRI := LVLB
:call gen_bottle

vf := delay
v8 := 253
v8 -= vf

i := str
bcd v8

v8 := 4
v3 := 65

i := str
load v2

i := hex v0
sprite v3 v8 5
v3 += 5
i := hex v1
sprite v3 v8 5
v3 += 5
i := hex v2
sprite v3 v8 5

vf := 9
i := hex vf
v0 := 0
v8 := 55
sprite v0 v8 5

# draw play fields
: _fieldr
v1 := 6

i := b00010001

: _field
:next b00010001
sprite v0 v1 1

v0 += 64
v1 += 9
sprite v0 v1 1

if v1 == 57
then jump _fieldc

v0 -= 64
v1 -= 3
jump _field

: _fieldc
if v0 == 120
then jump _fieldd

v0 -= 56
jump _fieldr

: _fieldd
vf := key
jump _main

: gen_bottle
i := zeros
load v9
i := hand

: _gen_bottle_clear
save v7
v8 += 8
i += v9
if v8 != 128
then jump _gen_bottle_clear

v2 := 0xff
i := bottles_40
save v2

i := ltbl
i += VIRI
load v0
LVL := v0

VIRI += 1
VIRI <<= VIRI
VIRI <<= VIRI

LFSRM := LFSR0
LFSRL := LFSR1

FOURTEEN := 14
FOUR := 4
ENDL := 0x80

: _gen_bottle_loop
# pick level in bottle
LFSRM <<= LFSRM
LFSRL <<= LFSRL
LFSRM |= FLAG
SPARE := 0x80
SPARE &= LFSRM
FLAG := 0x80
FLAG &= LFSRL
SPARE += FLAG
SPARE <<= SPARE
LFSRL |= FLAG

POS := 0x7f
POS &= LFSRL
if POS < LVL
then jump _gen_bottle_loop

if POS >= ENDL
then jump _gen_bottle_loop

: _gen_bottle_retry
i := bottle
i += POS
load v0

if v0 == 0
then jump _gen_bottle_found_empty

: _gen_bottle_try_again
:next b00000001
POS += 1
if POS != ENDL
then jump _gen_bottle_retry

ENDL := 0x7f
ENDL &= LFSRL
ENDL += 1
jump _gen_bottle_loop

: _gen_bottle_found_empty
i := bottle-16
i += POS
load v0
MSK := v0

TYPE := 7
TYPE &= POS

i += FOURTEEN
load v0
if TYPE > 1
then MSK |= v0

i += FOUR
load v0
if TYPE < 7
then MSK |= v0

i += FOURTEEN
load v0
MSK |= v0

if MSK == 7
then jump _gen_bottle_try_again

TYPE := 3
TYPE &= VIRI
if TYPE != 3
then jump _gen_bottle_type_known
SPARE := 0xf
SPARE &= LFSRM
i := vtbl
i += SPARE
load v0
TYPE := v0

: _gen_bottle_type_known
if TYPE == 0
then TYPE := 4

: _gen_bottle_find_type
v0 := TYPE
v0 &= MSK

if v0 == 0
then jump _gen_bottle_success

TYPE >>= TYPE
if FLAG != 0
then TYPE := 4
jump _gen_bottle_find_type

: _gen_bottle_success
v0 := TYPE
i := bottle
i += POS
save v0

VIRI -= 1
i := b00000001
sprite VIRI YPOS 1

if VIRI == 0
then return

jump _gen_bottle_loop

: ltbl
0x30 0x30 0x30 0x30 0x30 0x30 0x30
0x30 0x30 0x30 0x30 0x30 0x30 0x30
0x30 0x28 0x28 0x20 0x20 0x18 0x18

: vtbl
0 1 2 2 1 0 0 1 2 2 1 0 0 1 2 1

:monitor bottle 280
