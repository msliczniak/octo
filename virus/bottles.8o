# A B T
# ---+-
# 0 0 E
# 0 1 V
# 1 0 R
# 1 1 L

# A B C
# ---+-
# 0 0 E
# 0 1 B
# 1 0 Y
# 1 1 R

:org 0x5b
: bottle-16
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

: bottle
0 0 0 0 0 0

: hand
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0

: guard0
                                 0
0x00 0x00

: bottle0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

: guard1
0xff 0x00 0x00 0x00

: bottle1
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0
0 0 0 0  0 0 0 0  0 0 0 0  0 0 0 0

: guard2
0xff

: zeros
0 0 0 0  0 0 0 0

:org 0x200
: main
v0 := 0xff

: str
v9 := 0x12
va := 0x12

i := guard0
save v2

i := guard1
save v3

i := guard2
save va

:org 0x212
hires

: _main
clear

i := zeros
load v7
v8 := 0
v9 := 8
i := hand

: _main_clear
save v7
v8 += 8
i += v9
if v8 != 0
then jump _main_clear

:const SEEDM 0x0
:const SEEDL 0x1
:const LVLA 20
:const LVLB 18

:alias SPARE v0
:alias FOURTEEN v1
:alias LFSRM v2
:alias LFSRL v3
:alias FOUR  v4
:alias LVL   v5
:alias POS   v6
:alias TYPE  v7
:alias MSK   v8
:alias END   v9
:alias YPOS  va
:alias VIRI  vb
:alias EIGHTEEN vc
:alias LFSR0 vd
:alias LFSR1 ve
:alias FLAG  vf

LFSR0 := random 0x7f
LFSR1 := random 0xff

if LFSR0 != 0
then jump good_seed

if LFSR1 == 0
then LFSR1 := 1

: good_seed
#FSR0 := SEEDM
#FSR1 := SEEDL

v0 := 0
v7 := 55
v1 := LFSR0
:call draw_seed
v1 := LFSR1
:call draw_seed

FOURTEEN := 14
FOUR := 4
EIGHTEEN := 18

YPOS := 2
VIRI := LVLA
END := 0x7d
:call gen_bottle

vf := 255
delay := vf

: repeat
vf := delay
if vf != 253
then jump repeat
YPOS := 61
VIRI := LVLB
END := 0xf5
:call gen_bottle

vf := delay
v8 := 253
v8 -= vf

i := str
bcd v8

v8 := 4
v3 := 65

i := str
load v2

i := hex v0
sprite v3 v8 5
v3 += 5
i := hex v1
sprite v3 v8 5
v3 += 5
i := hex v2
sprite v3 v8 5

vf := key
jump _main

: gen_bottle
LFSRM := LFSR0
LFSRL := LFSR1
i := ltbl
i += VIRI
load v0
LVL := v0
MSK := 0x7f

VIRI += 1
VIRI <<= VIRI
VIRI <<= VIRI

: _gen_bottle_loop
# pick level in bottle
LFSRM <<= LFSRM
LFSRL <<= LFSRL
LFSRM |= FLAG
SPARE := 0x80
SPARE &= LFSRM
FLAG := 0x80
FLAG &= LFSRL
SPARE += FLAG
SPARE <<= SPARE
LFSRL |= FLAG

POS := LFSRL
POS &= MSK
# clamp biasing to blottom of bottle
POS -= LVL
if FLAG == 0
then POS += LVL
POS =- END

: _gen_bottle_retry
i := bottle
i += POS
load v0

if v0 == 0
then jump _gen_bottle_found_empty

: _gen_bottle_try_again
if POS == END
then jump _gen_bottle_loop

POS += 1
jump _gen_bottle_retry

: _gen_bottle_found_empty
# fail fast next time if neighbors too dense
:next b10000000
SPARE := 0x80
save SPARE

i := bottle-16
i += POS
load v0
TYPE := v0

FLAG := 6
FLAG &= POS
if FLAG == 4
then jump _gen_bottle_l

if FLAG == 6
then jump _gen_bottle_r

i += FOURTEEN
load v0
TYPE |= v0

i += FOUR
: _gen_bottle_r_ret
load v0
TYPE |= v0

i += FOURTEEN
: _gen_bottle_l_ret
load v0
TYPE |= v0

# remove msb in case empty was marked dense
TYPE <<= TYPE
if TYPE == 14
then jump _gen_bottle_try_again

TYPE >>= TYPE
SPARE := 3
SPARE &= VIRI
if SPARE != 3
then jump _gen_bottle_type_known

SPARE := 0xf
SPARE &= LFSRM
i := vtbl
i += SPARE
load v0
SPARE := v0

: _gen_bottle_type_known
if SPARE == 0
then SPARE := 4

FLAG := SPARE

: _gen_bottle_find_type
SPARE &= TYPE

if v0 == 0
then jump _gen_bottle_success

SPARE >>= SPARE
if FLAG != 0
then SPARE := 4
jump _gen_bottle_find_type

: _gen_bottle_success
SPARE := FLAG
i := bottle
i += POS
save SPARE

VIRI -= 1
i := b10000000
sprite VIRI YPOS 1

if VIRI == 0
then return

jump _gen_bottle_loop

: _gen_bottle_l
i += FOURTEEN
load v0
TYPE |= v0

i += EIGHTEEN
jump _gen_bottle_l_ret

: _gen_bottle_r
i += EIGHTEEN
jump _gen_bottle_r_ret

: draw_seed
v2 := 0xf
v2 &= v1
v1 >>= v1
v1 >>= v1
v1 >>= v1
v1 >>= v1
i := hex v1
sprite v0 v7 5
v0 += 5
i := hex v2
sprite v0 v7 5
v0 += 5
return

: ltbl
79 79 79 79 79 79 79
79 79 79 79 79 79 79
79 87 87 95 95 103 103

: vtbl
0 1 2 2 1 0 0 1 2 2 1 0 0 1 2 1

:monitor hand 258
:monitor bottle0 141
