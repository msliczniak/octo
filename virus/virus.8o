# https://tetrisconcept.net/threads/dr-mario-virus-placement.2037/page-2#post-63038

# problematic seeds
#
#:const seed0 0
#:const seed1 0
#
#:const seed0 0
#:const seed1 1

:alias lfsr0		vD
:alias lfsr1		vE
:alias c0				v3
:alias c1				v4
:alias s0				v5
:alias s1				v6
:alias s2				v7
:alias x				v8
:alias y				v9
:alias viruses	vA
:alias level		vB

:alias pos x
:alias type y
:alias surrounding c0

: main
hires

#:breakpoint main

: gen
v0 := 0xff
v1 := 0xff
v2 := 0xff
v3 := 0xff
v4 := 0xff
v5 := 0xff
v6 := 0xff
v7 := 0xff
v8 := 0xff
v9 := 0xff
vA := 0xff
vB := 0xff
vC := 0xff
vD := 0xff
vE := 0xff
vF := 0xff
i := bottle0
save vF
i := bottle1
save vF
i := bottle2
save vF
i := bottle3
save vF
i := bottle4
save vF
i := bottle5
save vF
i := bottle6
save vF
i := bottle7
save vF

# bottle is now empty

i := bottle0
v0 := 0
v1 := 16
v2 := 32
v3 := 48
v4 := 64
v5 := 80
v6 := 96
v7 := 112

sprite v4 v0 0
sprite v4 v1 0
sprite v4 v2 0
sprite v4 v3 0

sprite v5 v0 0
sprite v5 v1 0
sprite v5 v2 0
sprite v5 v3 0

sprite v6 v0 0
sprite v6 v1 0
sprite v6 v2 0
sprite v6 v3 0

sprite v7 v0 0
sprite v7 v1 0
sprite v7 v2 0
sprite v7 v3 0

# p2 inverted

lfsr0 := random 0xff

# every other seed generates same stream
# makes test below simpler to force even
lfsr1 := random 0xfe

if lfsr1 != 0
then jump good_seed

# force a valid seed
if lfsr0 == 0
then lfsr0 := 0xff

: good_seed

#lfsr0 := 0xa5
#lfsr1 := 0x5a

# game level
# v0 := 20
v0 := 9
viruses := v0
viruses += 1
viruses <<= viruses
viruses <<= viruses
i := virus_level_tbl
i += v0
load v0
level := v0

: gen_loop

# pick level in bottle
:call lfsr

#:breakpoint gen_loop

s0 := 0xf
s0 &= lfsr0
if s0 > level
then jump gen_loop

# level in bottle is not too high

pos := 7
pos &= lfsr1
i := virus_pos_tbl
i += s0
load v0
pos += v0

# virus position now set

type := 3
type &= viruses
if type != 3
then jump type_known
:call lfsr
s0 := 0xf
s0 &= lfsr1 
i := virus_type_tbl
i += s0
load v0
type := v0

: type_known

# we'll need these masks soon
s0 := 3
s1 := 7

: retry

i := bottle0
i += pos
load v0

if v0 == 0xff
then jump found_empty

: try_again

pos += 1
if pos == 0x80
then jump gen_loop

jump retry

: found_empty

surrounding := 0

i := bottle
i += pos
load v0
v0 &= s0
i := bit_tbl
i += v0
load v0
surrounding |= v0

i := bottle1
i += pos
load v0
v0 &= s0
i := bit_tbl
i += v0
load v0
surrounding |= v0

s2 := pos
s2 &= s1

if s2 == 0
then jump ok0
if s2 == 1
then jump ok0

i := bottlem2
i += pos
load v0
v0 &= s0
i := bit_tbl
i += v0
load v0
surrounding |= v0

: ok0

if s2 == 6
then jump ok1
if s2 == 7
then jump ok1

i := bottlep2
i += pos
load v0
v0 &= s0
i := bit_tbl
i += v0
load v0
surrounding |= v0

: ok1

if surrounding == 7
then jump try_again

: find_type

i := bit_tbl
i += type
load v0
v0 &= surrounding
if v0 == 0
then jump success

if type == 0
then type := 3
type -= 1
jump find_type

: success

v0 := 0xd0
v0 |= type
i := bottle0
i += pos
save v0

#:breakpoint ani

# v column *4
s2 := 0x78
s2 &= pos
s2 >>= s2

# h row *8
s0 := 7
s0 &= pos
s0 <<= s0
s0 <<= s0
s0 <<= s0

i := yellow
type <<= type
type <<= type
i += type
sprite s0 s2 4
s0 += 64
sprite s0 s2 4

viruses -= 1
if viruses != 0
then jump gen_loop

# pause

s0 := 15
buzzer := s0
s0 := key
clear
jump gen

: lfsr
c1 := 0

s2 := 2

s0 := lfsr0
s0 &= s2

s1 := lfsr1
s1 &= s2

s0 ^= s1

if s0 != 0
then c1 := 1

# 1
c0 := 1
c0 &= lfsr0

s0 := 0
if c1 != 0
then s0 := 128

lfsr0 >>= lfsr0

lfsr0 |= s0

c1 := c0

#2
c0 := 1
c0 &= lfsr1

s0 := 0
if c1 != 0
then s0 := 128

lfsr1 >>= lfsr1

lfsr1 |= s0

return

: yellow
0xFE 0xFE 0xFE 0x00

: red
0xFE 0xEE 0xFE 0x00

: blue
0xFE 0xD6 0xFE 0x00

: virus_level_tbl
0x9 0x9 0x9 0x9 0x9 0x9 0x9
0x9 0x9 0x9 0x9 0x9 0x9 0x9
0x9 0xA 0xA 0xB 0xB 0xC 0xC
0xC 0xC 0xC 0xC 0xC 0xC 0xC
0xC 0xC 0xC 0xC 0xC 0xC 0xC

: virus_pos_tbl
0x78 0x70 0x68 0x60 0x58 0x50 0x48 0x40
0x38 0x30 0x28 0x20 0x18 0x10 0x08 0x00

: bit_tbl
1 2 4 0

: virus_type_tbl
0 1 2 2 1 0 0 1 2 2 1 0 0 1 2 1

: bottle
0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff

: bottlem2
0xff 0xff

: bottle0
0 1

: bottlep2

2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle1
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle2
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle3
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle4
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle5
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle6
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

: bottle7
0 1 2 3 4 5 6 7 8 9 0xa 0xb 0xc 0xd 0xe 0xf

0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff 0xff

#:monitor bottle 160

# 16x16 4c sprite showing squished drop animation
#0x3F 0x80 0x3B 0x80 0x31 0x80 0x3F 0x80 0x07 0xC0 0x06 0xE0 0x04 0x60 0x07 0xC0 0xFE 0x00 0xEE 0x00 0xC6 0x00 0xFE 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x7E 0x00 0x77 0x00 0x63 0x00 0x7E 0xFF 0xC0 0xDE 0xE0 0x8C 0x60 0xFF 0xC0 0x01 0xF8 0x01 0xDC 0x01 0x8C 0x01 0xF8 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00
